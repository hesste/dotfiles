# -*- Mode: sh -*-


# chpwd_profiles(): Directory Profiles, Quickstart:
#
# In .zshrc.local:
#
#   zstyle ':chpwd:profiles:/usr/src/grml(|/|/*)'   profile grml
#   zstyle ':chpwd:profiles:/usr/src/debian(|/|/*)' profile debian
#   chpwd_profiles
#
# For details see the `grmlzshrc.5' manual page.
function chpwd_profiles() {
    local profile context
    local -i reexecute
    
    context=":chpwd:profiles:$PWD"
    # see man zshmodules
    zstyle -s "$context" profile profile || profile='default'
    zstyle -T "$context" re-execute && reexecute=1 || reexecute=0

    # test if CWPWD_PROFILE variable does not exist
    if (( ${+parameters[CHPWD_PROFILE]} == 0 )); then
        typeset -g CHPWD_PROFILE
        local CHPWD_PROFILES_INIT=1
        (( ${+functions[chpwd_profiles_init]} )) && chpwd_profiles_init
    elif [[ $profile != $CHPWD_PROFILE ]]; then
        # test if function chpwd_leave_profile_$CHPWD_PROFILE exist and execute
        (( ${+functions[chpwd_leave_profile_$CHPWD_PROFILE]} )) \
            && chpwd_leave_profile_${CHPWD_PROFILE}
    fi
    if (( reexecute )) || [[ $profile != $CHPWD_PROFILE ]]; then
        (( ${+functions[chpwd_profile_$profile]} )) && chpwd_profile_${profile}
    fi

    CHPWD_PROFILE="${profile}"
    return 0
}


typeset -gaU chpwd_functions
chpwd_functions+=chpwd_profiles


# define profiles based on directories:


## exemple for profile 'test':

# zstyle ":chpwd:profiles:$HOME/tmp(|/|/*)"       profile test
# function chpwd_profile_test() {
    # [[ ${profile} == ${CHPWD_PROFILE} ]] && return 1
    # print "chpwd(): Switching to profile: $profile"
    # export PS1="[ $profile ] $PS1"
# }
# function chpwd_leave_profile_test() {
    # print "chpwd(): leaving profile: $CHPWD_PROFILE"
    # export PS1=${PS1/\[ $CHPWD_PROFILE \] /}
# }

